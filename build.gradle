buildscript {
	repositories {
		maven {
			url 'https://m2repo.system.local/content/groups/full/'
		}
	}

	dependencies {
		// update requires scheme update in sda-dependencycheck.system.local
		classpath 'org.postgresql:postgresql:42.7.7'// this is necessary for owasp dependency check
	}
}

plugins {
	id 'idea'
	id 'jacoco'
	id 'org.sonarqube' version '6.2.0.5505'
	id 'org.owasp.dependencycheck' version '12.1.3'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id("org.openrewrite.rewrite") version("latest.release")
}


rewrite {
	activeRecipe("de.signaliduna.elpa.UpgradeToSpringBoot4")
	setExportDatatables(true)
	configFile = file("config/rewrite.yml")
}


ext {
	set('springCloudVersion', "2025.0.0")
	set('micrometerTracingVersion', "1.5.3")
	set('jacksonVersion', "2.19.1")
	set('cloudeventsVersion', "4.0.1")
}

allprojects {
	group = 'de.signaliduna.elpa.dlt-manager'

	repositories {
		maven {
			url "https://m2repo.system.local/content/groups/full/"
		}
		mavenCentral()
	}
}

LinkedHashSet jacocoSet = new LinkedHashSet()

dependencies {
	// Spring Boot 4 migration recipes (community edition)
	// Includes: @MockBean→@MockitoBean, modular starters, Boot properties,
	//           Spring Framework 7, Security 7, Hibernate 7.1, SpringDoc 3.0,
	//           Testcontainers 2.x base renames
	rewrite("org.openrewrite.recipe:rewrite-spring:6.23.1")

	// Jackson 2.x → 3.x migration (separate module, NOT bundled in rewrite-spring)
	// Includes: package renames, type renames, method renames, dependency changes
	rewrite("org.openrewrite.recipe:rewrite-jackson:1.15.0")

	// Testcontainers 2.x migration (may already be transitively included)
	rewrite("org.openrewrite.recipe:rewrite-testing-frameworks:3.8.0")
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.sonarqube'
	apply plugin: 'org.owasp.dependencycheck'

	java {
		withSourcesJar()
		sourceCompatibility = JavaVersion.VERSION_21
	}


	test {
		useJUnitPlatform()
	}

	jacoco {
		toolVersion = "0.8.13"
		reportsDirectory = layout.buildDirectory.dir("jacoco")
	}

	jacocoTestReport {
		dependsOn test
		getExecutionData().setFrom(fileTree(layout.buildDirectory).include("/jacoco/*.exec"))
		reports {
			xml.required = true
			csv.required = false
			html.required = false
		}
	}

	jacocoTestCoverageVerification {
		dependsOn jacocoTestReport
		violationRules {
			rule {
				enabled = true
				element = 'CLASS'
				excludes = ['de.signaliduna.elpa.dltmanager.adapter.message.errorhandler.model.*']
				limit {
					counter = 'BRANCH'
					value = 'COVEREDRATIO'
					minimum = 1.0
				}
			}
		}
	}

	jacocoSet.addAll(
		tasks.findAll({ it.name.contains('jacocoTestReport') || it.name == 'test' })
	)

	tasks.named('test') {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
		jvmArgs("-XX:+EnableDynamicAgentLoading")
	}

	check.dependsOn jacocoTestCoverageVerification
	dependencyCheck {
		outputDirectory = layout.buildDirectory.asFile.get().path + "/reports/dependencycheck"
		format = "XML"
		autoUpdate = false
		failOnError = false
		suppressionFiles = ["https://git.system.local/projects/SDA/repos/global-cve-suppression/raw/global-suppressed-cves.xml?at=refs%2Fheads%2Fmaster"]
		data {
			connectionString = "jdbc:postgresql://vipsiae11p.system.local:5432/depchkp?currentSchema=v12"
			driver = "org.postgresql.Driver"
			username = "ZA-SVC-DEPCHK-P"
			password = "QPmWX.CZ4hbJRtU"
			directory = rootProject.layout.buildDirectory.dir("owasp-data").get()
		}

		analyzers {
			centralEnabled = false
			nexusEnabled = false
		}
	}
}

rootProject.tasks.sonarqube.dependsOn = jacocoSet

sonarqube {
	properties {
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.coverage.exclusions", "**/model/*.*"
		property "sonar.cpd.exclusions", "**/model/*.*, **/mongo-init/*.js"
	}
}

tasks.register('printVersion') {
	println(version)
}

final String SNAPSHOT_SUFFIX = "-SNAPSHOT"
final String GRADLE_PROPERTIES_FILE_NAME = "gradle.properties"

tasks.register('removeSnapshot', WriteProperties) {
	destinationFile = file(GRADLE_PROPERTIES_FILE_NAME)
	def newVersion = version.replace(SNAPSHOT_SUFFIX, "")
	println('new version: ' + newVersion)
	property('version', newVersion)
}

tasks.register('increasePatchVersion', WriteProperties) {
	destinationFile = file(GRADLE_PROPERTIES_FILE_NAME)
	println('old version: ' + version)
	def versionStr = version as String
	boolean isSnapshot = versionStr.endsWith(SNAPSHOT_SUFFIX)
	def versionNo = (isSnapshot) ? versionStr.replace(SNAPSHOT_SUFFIX, "") : versionStr
	def splitVersion = versionNo.split("\\.")
	def patchVersion = Integer.valueOf(splitVersion[2]).intValue() + 1
	def newVersion = String.format('%s.%s.%d', splitVersion[0], splitVersion[1], patchVersion) + ((isSnapshot) ? SNAPSHOT_SUFFIX : "")
	println('new version: ' + newVersion)
	property('version', newVersion)
}

tasks.register('addSnapshot', WriteProperties) {
	destinationFile = file(GRADLE_PROPERTIES_FILE_NAME)
	def newVersion = version + SNAPSHOT_SUFFIX
	println('new version: ' + newVersion)
	property('version', newVersion)
}

